apply plugin: eclipsebuild.BundlePlugin

dependencies {
    compile withEclipseBundle('org.eclipse.core.runtime')
    compile withEclipseBundle('org.eclipse.core.resources')
    compile withEclipseBundle('org.eclipse.core.variables')
    compile withEclipseBundle('org.eclipse.core.filesystem')
    compile withEclipseBundle('org.eclipse.jdt.core')
    compile withEclipseBundle('org.eclipse.jdt.junit.core')
    compile withEclipseBundle('org.eclipse.jdt.launching')
    compile withEclipseBundle('org.eclipse.debug.core')

    bundled "com.gradleware.tooling:toolingmodel:$commonsLibVersion"
    bundled "org.slf4j:slf4j-simple:$slf4jLibVersion"
}

// append the sources of each first-level dependency and its transitive dependencies of
// the 'bundled' configuration to the 'bundledSource' configuration
configurations.bundled.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
    addSourcesRecursively(dep)
}

def addSourcesRecursively(dep) {
    dependencies {
        bundledSource group: dep.moduleGroup, name: dep.moduleName, version: dep.moduleVersion, classifier: 'sources'
    }
    dep.children.each { addSourcesRecursively(it) }
}
